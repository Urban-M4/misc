configfile: workflow.source_path("config.yaml")
envvars: "NETCDF"

rule ALL_EXPERIMENTS:
    input: expand("{experiment}/finished.clean", experiment=config["experiments"].keys())

rule CREATE_WORKDIR:
    input:
        namelist_wps = workflow.source_path("namelist.wps"),
        namelist_input = workflow.source_path("namelist.input"),
        wrf_job = workflow.source_path("wrf.job")
    output:
        "{experiment}/namelist.wps",
        "{experiment}/GEOGRID.TBL",
        "{experiment}/namelist.input"
    params:
        # experiment = lambda wildcards: config['experiments'][wildcards.experiment],
        geog_data_res = lambda wildcards: config['experiments'][wildcards.experiment]['geog_data_res'],
        geog_data_path = lambda wildcards: config['experiments'][wildcards.experiment]['geog_data_path'],
        num_land_cat = lambda wildcards: config['experiments'][wildcards.experiment]['num_land_cat'],
        geogrid_table = lambda wildcards: workflow.source_path(config['experiments'][wildcards.experiment]['geogrid_table']),
    shell:
        """
        mkdir -p {wildcards.experiment}
        cd {wildcards.experiment}
        cp {input.wrf_job} .
        cp {params.geogrid_table} GEOGRID.TBL

        # Read source namelist.wps with f90nml, make some changes, and save in work dir
        f90nml \
            -g geogrid \
            -v geog_data_path={params.geog_data_path} \
            {input.namelist_wps} namelist.wps

        f90nml \
            -g geogrid \
            -v geog_data_res={params.geog_data_res} \
            {input.namelist_wps} namelist.wps

        # Read source namelist.input, make some changes, and save in work dir
        f90nml \
            -g physics \
            -v num_land_cat={params.num_land_cat} \
            {input.namelist_input} namelist.input
        """


rule GEOGRID:
    input:
        "{experiment}/namelist.wps",
        geogrid = "{experiment}/GEOGRID.TBL"
    output:
        "{experiment}/finished.geogrid"
    shell:
        """
        cd {wildcards.experiment}

        ln -sf {input.geogrid} {config[wps_home]}
        {config[wps_home]}/geogrid.exe

        # Scan logfile for errors and raise if necessary (geogrid can fail silently with 0 exit status)
        grep "ERROR" geogrid.log && echo "Aborting: ERROR in geogrid.log." && exit 1

        touch finished.geogrid
        """

rule W2W:
    input:
        "{experiment}/finished.geogrid"
    output:
        "{experiment}/finished.w2w"
    shell:
        """
        cd {wildcards.experiment}
        w2w . /projects/0/prjs0914/wrf-data/default/lcz/amsterdam_lcz4_clean.tif ./geo_em.d04.nc v4.5.2
        mv geo_em.d01_61.nc geo_em.d01.nc
        mv geo_em.d02_61.nc geo_em.d02.nc
        mv geo_em.d03_61.nc geo_em.d03.nc
        mv geo_em.d04_LCZ_params.nc geo_em.d04.nc
        touch finished.w2w
        """

rule UNGRIB:
    input:
        "{experiment}/namelist.wps"
    output:
        "{experiment}/finished.ungrib"
    shell:
        """
        cd {wildcards.experiment}

        # Remove old output if present (ungrib doesn't like to overwrite stuff)
        rm -f FILE*

        # Link vtable
        cp {config[wps_home]}/ungrib/Variable_Tables/Vtable.ECMWF Vtable

        # Link gribfiles
        {config[wps_home]}/link_grib.csh {config[data_home]}/real-time/july2019/*

        # Run ungrib
        {config[wps_home]}/ungrib.exe

        # Scan logfile for errors and raise if necessary (ungrib can fail silently with 0 exit status)
        grep "ERROR" ungrib.log && echo "Aborting: ERROR in ungrib.log." && exit 1

        # Report ready
        touch finished.ungrib
         """

rule METGRID:
    input:
        "{experiment}/namelist.wps",
        "{experiment}/finished.ungrib",
        "{experiment}/finished.geogrid",
        "{experiment}/finished.w2w",  # TODO make conditional
    output: "{experiment}/finished.metgrid"
    shell:
        """
        cd {wildcards.experiment}

        cp {config[wps_home]}/metgrid/METGRID.TBL.ARW METGRID.TBL
        {config[wps_home]}/metgrid.exe

        # Scan logfile for errors and raise if necessary (metgrid can fail silently with 0 exit status)
        grep "ERROR" metgrid.log && echo "Aborting: ERROR in metgrid.log. " && exit 1

        touch finished.metgrid
        """

rule REAL:
    input:
        "{experiment}/namelist.input",
        "{experiment}/finished.metgrid"
    output:
        "{experiment}/finished.real"
    shell:
        """
        cd {wildcards.experiment}
        {config[wrf_home]}/run/real.exe
        touch finished.real
        """

rule WRF:
    input:
        "{experiment}/namelist.input",
        "{experiment}/finished.real"
    output:
        "{experiment}/finished.wrf"
    shell:
        """
        cd {wildcards.experiment}

        # TODO link instead of cp and unlink after finishing?
        cp -f {config[wrf_home]}/run/CAMtr_volume_mixing_ratio.RCP8.5 CAMtr_volume_mixing_ratio
        cp -f {config[wrf_home]}/run/ozone* .
        cp -f {config[wrf_home]}/run/RRTMG* .
        cp -f {config[wrf_home]}/run/*.TBL .

        # Submit wrf job to SLURM and wait for it to finish
        sbatch --wait wrf.job {config[wrf_home]}/run/wrf.exe

        # Report status
        touch finished.wrf
        """

rule REORGANIZE_OUTPUT:
    input:
        "{experiment}/finished.wrf"
    output:
        "{experiment}/finished.clean"
    shell:
        """
        cd {wildcards.experiment}

        mkdir -p output/geogrid
        mkdir -p output/ungrib
        mkdir -p output/metgrid
        mkdir -p output/real
        mkdir -p output/wrf

        mv geo_em* output/geogrid
        mv GRIBFILE.* output/ungrib
        mv FILE* output/ungrib
        mv met_em* output/metgrid
        mv wrfbdy* wrfinput* output/real
        mv wrfout* output/wrf

        # TODO: copy log files to separate folder?
        # Especially since real.exe logs are overwritten by wrf.exe (rsl.error.0000 and rsl.out.0000)

        touch finished.clean
        """


